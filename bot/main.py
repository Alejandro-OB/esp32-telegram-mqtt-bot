#include <WiFi.h>
#include <WiFiClientSecure.h>
#include <PubSubClient.h>
#include <ESPping.h>
#include "secrets.h"
#include <WebServer.h>
#include <Update.h>

WiFiClientSecure secureClient;
PubSubClient mqttClient(secureClient);
WiFiUDP udp;

WebServer otaServer(8266); // Puerto 8266 para OTA HTTP


unsigned long ultimaConexionExitosa = 0;
bool yaNotificadoReconexion = false;

// Declaraci√≥n de funciones
void estadoESP32();
void reiniciarESP32();
void reiniciarPC();
void mostrarAyuda();

void conectarWiFi() {
  WiFi.mode(WIFI_STA);
  WiFi.begin(ssid, password);
  if (!WiFi.config(local_IP, gateway, subnet, primaryDNS, secondaryDNS)) {
    Serial.println("‚ö†Ô∏è Error configurando IP est√°tica");
  }
  Serial.print("Conectando a WiFi");
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println("\n‚úÖ WiFi conectado");
  ultimaConexionExitosa = millis();
  yaNotificadoReconexion = false;

}

void conectarMQTT() {
  secureClient.setInsecure(); // No validaci√≥n de certificado (TLS simple)
  mqttClient.setServer(mqtt_broker, mqtt_port);

  while (!mqttClient.connected()) {
    Serial.print("Conectando a MQTT...");
    if (mqttClient.connect("ESP32Client", mqtt_user, mqtt_password)) {
      Serial.println("‚úÖ Conectado al broker MQTT");
      mqttClient.subscribe("mqtt/pc/encender");
      mqttClient.subscribe("mqtt/pc/verificar");
      mqttClient.subscribe("mqtt/pc/estado_esp32");
      mqttClient.subscribe("mqtt/pc/reiniciar_esp32");
      mqttClient.subscribe("mqtt/pc/apagar");
      mqttClient.subscribe("mqtt/pc/reiniciar_pc");
      mqttClient.subscribe("mqtt/pc/ayuda");
    } else {
      Serial.print("‚ùå Error: ");
      Serial.println(mqttClient.state());
      delay(2000);
    }
  }
}

// Funci√≥n para publicar en MQTT con verificaci√≥n de conexi√≥n
void publicarMQTT(const char* topic, const char* mensaje) {
  if (mqttClient.connected()) {
    mqttClient.publish(topic, mensaje);  
    Serial.printf("üì§ Publicando en %s: %s\n", topic, mensaje); 
  } else {
    // Si no est√° conectado, intenta reconectar
    Serial.println("‚ùå MQTT no est√° conectado. Intentando reconectar...");
    conectarMQTT();  
    mqttClient.publish(topic, mensaje);  
    Serial.printf("üì§ Publicando en %s despu√©s de reconectar: %s\n", topic, mensaje);
  }
}

void callback(char* topic, byte* payload, unsigned int length) {
  String mensaje;
  for (int i = 0; i < length; i++) mensaje += (char)payload[i];
  Serial.printf("üì© [%s] %s\n", topic, mensaje.c_str());

  if (String(topic) == "mqtt/pc/encender" && mensaje == "true") {
    encenderPC();
  }
  else if (String(topic) == "mqtt/pc/verificar" && mensaje == "true") {
    verificarPC();
  }
  else if (String(topic) == "mqtt/pc/estado_esp32" && mensaje == "true") {
    estadoESP32();
  }
  else if (String(topic) == "mqtt/pc/reiniciar_esp32" && mensaje == "true") {
    reiniciarESP32();
  }
  else if (String(topic) == "mqtt/pc/apagar" && mensaje == "true") {
    apagarPC();
  }
  else if (String(topic) == "mqtt/pc/reiniciar_pc" && mensaje == "true") {
    reiniciarPC();
  }
  else if (String(topic) == "mqtt/pc/ayuda" && mensaje == "true") {
    mostrarAyuda();
  }
  else {
    String mensajeError = "‚ùå Comando desconocido: " + String(topic) + ". Por favor, revisa el men√∫ de ayuda.";
    mqttClient.publish("mqtt/respuesta/error", mensajeError.c_str());
    mostrarAyuda();
    Serial.println(mensajeError);
  }

}

bool enviarWOL() {
  bool encendido = Ping.ping(ipPC);  // Verifica si el PC responde al pin

  if (encendido) {
    Serial.println("‚úÖ El PC ya est√° encendido. No es necesario enviar el paquete WOL.");
    publicarMQTT("mqtt/respuesta/verificar", "‚úÖ El PC ya est√° encendido.");
    return false;  
  } else {
    byte packet[102];
    memset(packet, 0xFF, 6);  
    for (int i = 1; i <= 16; i++) memcpy(&packet[i * 6], macPC, 6);  

    udp.begin(WiFi.localIP(), 9);
    udp.beginPacket(broadcastIP, 9);  
    udp.write(packet, sizeof(packet));  

    if (udp.endPacket() == 1) {
      Serial.println("‚ö° Paquete WOL enviado exitosamente.");
      publicarMQTT("mqtt/respuesta/verificar", "‚ö° Paquete WOL enviado para encender el PC.");   
      return true;  
    } else {
      Serial.println("‚ùå Error al enviar el paquete WOL.");
      publicarMQTT("mqtt/respuesta/verificar", "‚ùå Error al enviar el paquete WOL.");
      return false;  
    }
  }
}

void setup() {
  Serial.begin(115200);
  conectarWiFi();
  mqttClient.setCallback(callback);
  conectarMQTT();
  iniciarServidorOTA();
  publicarMQTT("mqtt/respuesta/reconexion_wifi", ("‚úÖ Conexi√≥n WiFi completada. IP: " + WiFi.localIP().toString()).c_str());
}

void loop() {
  
  void reconectarWiFi();

  if (!mqttClient.connected()) {
    Serial.println("‚ùå Desconectado de MQTT. Intentando reconectar...");
    conectarMQTT();  // Llama a la funci√≥n de reconexi√≥n
  }
  
  mqttClient.loop();

  otaServer.handleClient();
  
}

void iniciarServidorOTA() {
  otaServer.on("/", HTTP_GET, []() {
    otaServer.send(200, "text/html", R"rawliteral(
      <form method='POST' action='/update' enctype='multipart/form-data'>
        <input type='file' name='update'>
        <input type='submit' value='Actualizar'>
      </form>
    )rawliteral");
  });

  otaServer.on("/update", HTTP_POST, []() {
    otaServer.sendHeader("Connection", "close");
    otaServer.send(200, "text/plain", (Update.hasError()) ? "Fallo en actualizaci√≥n" : "Actualizaci√≥n correcta. Reiniciando...");
    delay(1000);
    ESP.restart();
  }, []() {
    HTTPUpload& upload = otaServer.upload();
    if (upload.status == UPLOAD_FILE_START && !Update.begin()) Update.printError(Serial);
    if (upload.status == UPLOAD_FILE_WRITE && Update.write(upload.buf, upload.currentSize) != upload.currentSize) Update.printError(Serial);
    if (upload.status == UPLOAD_FILE_END && !Update.end(true)) Update.printError(Serial);
  });

  otaServer.begin();
  Serial.println("‚úÖ Servidor OTA iniciado en puerto 8266");
  publicarMQTT("mqtt/respuesta/ota_estado", "‚úÖ Servidor OTA activo en puerto 8266");
}

void verificarPC() {
  bool encendido = Ping.ping(ipPC);
  String estado = encendido ? "‚úÖ PC ENCENDIDO" : "‚ùå PC APAGADO";
  Serial.println("üì° Verificaci√≥n: " + estado);
  publicarMQTT("mqtt/respuesta/verificar", estado.c_str());
  mqttClient.loop();
  delay(100);
}

void encenderPC() {
  if (enviarWOL()) {
    delay(12000);
    bool verificado = false;
    for (int i = 0; i < 5; i++) {
      delay(3000);
      if (Ping.ping(ipPC)) {
        verificado = true;
        break;
      }
    }
    if (verificado){
      publicarMQTT("mqtt/respuesta/verificar", "‚úÖ PC ENCENDIDO por WOL");
      Serial.println("‚úÖ PC ENCENDIDO por WOL");

    } else{
      publicarMQTT("mqtt/respuesta/verificar", "‚ö†Ô∏è WOL enviado pero el PC no respondi√≥ tras varios intentos"); 
      Serial.println("‚ö†Ô∏è WOL enviado pero el PC no respondi√≥ tras varios intentos");     
    } 
    
  } else {
    publicarMQTT("mqtt/respuesta/verificar", "‚ùå Error al enviar paquete WOL"); 
    Serial.println("‚ùå Error al enviar paquete WOL");
  }
}

void apagarPC() {
  WiFiClient client;
  if (client.connect(ipPC, PUERTO_FLASK)) {
    client.print("GET /apagar?token=miclave123 HTTP/1.1\r\nHost: " + ipPC.toString() + "\r\nConnection: close\r\n\r\n");
    delay(5000);  // Espera para permitir que el PC se apague

    // Verificaci√≥n de si el PC responde al ping
    if (!Ping.ping(ipPC)) {
      // Si el PC est√° apagado
      publicarMQTT("mqtt/respuesta/apagar", "üîå PC apagado correctamente.");
      Serial.println("üîå PC apagado correctamente.");
    } else {
      // Si el PC no est√° apagado correctamente
      publicarMQTT("mqtt/respuesta/apagar", "‚ö†Ô∏è El PC no se apag√≥ correctamente.");
      Serial.println("‚ö†Ô∏è El PC no se apag√≥ correctamente.");
    }  
  } else {
    // Si no se pudo conectar al servidor Flask para apagar
    publicarMQTT("mqtt/respuesta/apagar", "‚ùå No se pudo conectar al servidor Flask para apagar.");
    Serial.println("‚ùå No se pudo conectar al servidor Flask para apagar.");
  }
}


void reiniciarPC() {
  WiFiClient client;
  if (client.connect(ipPC, PUERTO_FLASK)) {
    client.print("GET /reiniciar?token=miclave123 HTTP/1.1\r\nHost: " + ipPC.toString() + "\r\nConnection: close\r\n\r\n");
    delay(15000);
    bool verificado = false;
    for (int i = 0; i < 5; i++) {
      delay(3000);
      if (Ping.ping(ipPC)) {
        verificado = true;
        break;
      }
    }
    if (verificado) {
      publicarMQTT("mqtt/respuesta/reiniciar_pc", "‚ôªÔ∏è PC reiniciado correctamente.");
      Serial.println("‚ôªÔ∏è PC reiniciado correctamente.");
    }
    else {
      publicarMQTT("mqtt/respuesta/reiniciar_pc", "‚ö†Ô∏è El PC no respondi√≥ despu√©s del reinicio.");
      Serial.println("‚ö†Ô∏è El PC no respondi√≥ despu√©s del reinicio.");
    }
  } else {
    publicarMQTT("mqtt/respuesta/reiniciar_pc", "‚ùå No se pudo conectar al servidor Flask para reiniciar.");
    Serial.println("‚ùå No se pudo conectar al servidor Flask para reiniciar."); 
  }
}

void mostrarAyuda() {
  String ayuda = "üÜò *Men√∫ de ayuda disponible:*\n\n";
  ayuda += "/encender_pc\n";
  ayuda += "/apagar_pc\n";
  ayuda += "/reiniciar_pc\n";
  ayuda += "/verificar_pc\n";
  ayuda += "/estado_esp32\n";
  ayuda += "/reiniciar_esp32\n";
  ayuda += "/actualizar_ota\n";

  publicarMQTT("mqtt/respuesta/ayuda", ayuda.c_str());
  Serial.println("üì§ Men√∫ de ayuda enviado por MQTT.");
}

void estadoESP32() {
  unsigned long uptime = millis() / 1000;
  int horas = uptime / 3600;
  int minutos = (uptime % 3600) / 60;
  int segundos = uptime % 60;

  int rssi = WiFi.RSSI();
  String calidad;

  if (rssi >= -60) {
    calidad = "Excelente";
  } else if (rssi >= -70) {
    calidad = "Buena";
  } else if (rssi >= -80) {
    calidad = "Regular";
  } else {
    calidad = "Mala";
  }

  String info = "üìä Estado del ESP32:\n";
  info += "- IP: " + WiFi.localIP().toString() + "\n";
  info += "- WiFi: " + WiFi.SSID() + " (" + String(rssi) + " dBm, " + calidad + " se√±al)\n";
  info += "- Uptime: " + String(horas) + "h " + String(minutos) + "m " + String(segundos) + "s";

  Serial.println(info);
  publicarMQTT("mqtt/respuesta/estado_esp32", info.c_str());
}


void reconectarWiFi() {
  if (WiFi.status() != WL_CONNECTED) {
    Serial.println("‚ùå WiFi desconectado. Intentando reconectar...");
    
    WiFi.disconnect();  // Asegura un nuevo intento limpio
    WiFi.begin(ssid, password);
    
    unsigned long inicio = millis();
    while (WiFi.status() != WL_CONNECTED && millis() - inicio < 10000) { // espera m√°ximo 10s
      delay(500);
      Serial.print(".");
    }

    if (WiFi.status() == WL_CONNECTED) {
      Serial.println("\n‚úÖ WiFi reconectado exitosamente.");
      publicarMQTT("mqtt/respuesta/reconexion_wifi", ("‚úÖ Reconexi√≥n WiFi completada."));
      yaNotificadoReconexion = false;
      ultimaConexionExitosa = millis();
    } else {
      Serial.println("\n‚ö†Ô∏è No se pudo reconectar al WiFi.");
    }
  }
}

void reiniciarESP32() {
  Serial.println("‚ôªÔ∏è Reiniciando ESP32...");
  publicarMQTT("mqtt/respuesta/reiniciar_esp32", "‚ôªÔ∏è Reiniciando ESP32...");
  delay(1000);  // Permitir que se env√≠e el mensaje antes de reiniciar
  ESP.restart();  // Reinicia el microcontrolador
}
